namespace: SysmelC definition: {
public: [


    Class: MidConstant withSuperclass: MidValue fields: #{
    }.

    Class: MidConstantInteger withSuperclass: MidConstant fields: #{
        value: Integer
    }.
    Class: MidConstantCharacter withSuperclass: MidConstant fields: #{
        value: Integer
    }.
    Class: MidConstantFloat withSuperclass: MidConstant fields: #{
        value: Float
    }.
    Class: MidConstantString withSuperclass: MidConstant fields: #{
        value: String
    }.
    Class: MidConstantSymbol withSuperclass: MidConstant fields: #{
        value: Symbol
    }.
    Class: MidConstantType withSuperclass: MidConstant fields: #{
        value: SysmelType
    }.
    Class: MidConstantValue withSuperclass: MidConstant fields: #{
        value: Value
    }.
    Class: MidGlobalValue withSuperclass: MidConstant fields: #{
        name: Symbol
    }.


    Class: MidControlNode withSuperclass: MidValue fields: #{
    }.

    Class: MidStartNode withSuperclass: MidControlNode fields: #{
        arguments: Array.
        captures: Array.
    }.

    Class: MidStopNode withSuperclass: MidControlNode fields: #{
        exitPoints: Array
    }.

    Class: MidClosureFunction withSuperclass: MidGlobalValue fields: #{
        captures: Array.
        arguments: Array.
        startNode: MidStartNode.
        stopNode: MidStopNode
    }.
    
    Class: MidFunctionLocalValue withSuperclass: MidValue fields: #{
        owner: MidClosureFunction
    }.

    Class: MidArgument withSuperclass: MidFunctionLocalValue fields: #{
        index: UInt32.
        name: Symbol.
        typeExpression: MidValue
    }.

    Class: MidCapture withSuperclass: MidFunctionLocalValue fields: #{
        index: UInt32.
        typeExpression: MidValue
    }.

    Class: MidReturn withSuperclass: MidControlNode fields: #{
        resultValue: MidValue
    }.

    Class: MidCallFunction withSuperclass: MidControlNode fields: #{
        previousControl: MidValue.
        functional: MidValue.
        selector: MidValue.
        arguments: Array.
    }.

    Class: MidSendMessage withSuperclass: MidControlNode fields: #{
        previousControl: MidValue.
        receiver: MidValue.
        selector: MidValue.
        arguments: Array.
    }.

    Record: MidFunctionBuilder withFields: #{
        midFunction: MidClosureFunction.
        currentControlNode: MidControlNode.
        exitPoints: OrderedCollection.
        sourcePosition: SourcePosition.
    }.
].

MidValue
    withSelector: #addUser: addMethod: {
        | $(MidValue)self $(MidValue)user |
        if: self users isNull then: {
            self users: OrderedCollection()
        }.
        self users add: user
    }.

MidStartNode
    withSelector: #connectWithUsed addMethod: {
        | $(MidStartNode)self :: Void |
        {
            $!i := 0.
            $argumentCount := self arguments size.
            while: (i < argumentCount) do:
            {
                (self arguments at: i) addUser: self
            } continueWith: (i := i + 1).
        }.
        {
            $!i := 0.
            $captureCount := self captures size.
            while: (i < captureCount) do:
            {
                (self captures at: i) addUser: self
            } continueWith: (i := i + 1).
        }
    }.

MidCallFunction
    withSelector: #connectWithUsed addMethod: {
        | $(MidCallFunction)self :: Void |
        self functional addUser: self.
        {
            $!i := 0.
            $argumentCount := self arguments size.
            while: (i < argumentCount) do:
            {
                (self arguments at: i) addUser: self
            } continueWith: (i := i + 1).
        }.
    }.

MidSendMessage
    withSelector: #connectWithUsed addMethod: {
        | $(MidSendMessage)self :: Void |
        self receiver addUser: self.
        self selector addUser: self.
        {
            $!i := 0.
            $argumentCount := self arguments size.
            while: (i < argumentCount) do:
            {
                (self arguments at: i) addUser: self
            } continueWith: (i := i + 1).
        }.
    }.

MidClosureFunction
    withSelector: #makeBuilder addMethod: {
        | $(MidClosureFunction) self :: MidFunctionBuilder |
        $startNode := MidStartNode#{
            captures: self captures.
            arguments: self arguments.
        }.
        startNode connectWithUsed.
        self startNode: startNode.

        $builder := MidFunctionBuilder#{
            midFunction: self.
            currentControlNode: startNode.
            exitPoints: OrderedCollection()
        }.
        builder
    }.

MidFunctionBuilder
    withSelector: #returnValue: addMethod: {
        | $(MidFunctionBuilder)self $(MidValue)resultValue :: MidReturnInstruction |
        $instruction := MidReturn#{
            sourcePosition: self sourcePosition.
            users: OrderedCollection().
            type: getBasicIntrinsicTypes() voidType.
            resultValue: resultValue.
        }.
        resultValue addUser: instruction.
        self exitPoints add: instruction.
        return: instruction
    };
    withSelector: #constantInteger: addMethod: {
        | $(MidFunctionBuilder)self $(Integer)constantValue :: MidConstantInteger |
        MidConstantInteger#{
            sourcePosition: self sourcePosition.
            type: getBasicIntrinsicTypes() integer.
            value: constantValue
        }.
    };
    withSelector: #constantCharacter: addMethod: {
        | $(MidFunctionBuilder)self $(Character)constantValue :: MidConstantCharacter |
        MidConstantCharacter#{
            sourcePosition: self sourcePosition.
            type: getBasicIntrinsicTypes() character.
            value: constantValue
        }.
    };
    withSelector: #constantFloat: addMethod: {
        | $(MidFunctionBuilder)self $(Float)constantValue :: MidConstantFloat |
        MidConstantFloat#{
            sourcePosition: self sourcePosition.
            type: getBasicIntrinsicTypes() float.
            value: constantValue
        }.
    };
    withSelector: #constantString: addMethod: {
        | $(MidFunctionBuilder)self $(String)constantValue :: MidConstantString |
        MidConstantString#{
            sourcePosition: self sourcePosition.
            type: getBasicIntrinsicTypes() string.
            value: constantValue
        }.
    };
    withSelector: #constantSymbol: addMethod: {
        | $(MidFunctionBuilder)self $(Symbol)constantValue :: MidConstantSymbol |
        MidConstantSymbol#{
            sourcePosition: self sourcePosition.
            type: getBasicIntrinsicTypes() symbol.
            value: constantValue
        }.
    };
    withSelector: #constantValue: addMethod: {
        | $(MidFunctionBuilder)self $(Value)constantValue :: MidConstantValue |
        MidConstantValue#{
            sourcePosition: self sourcePosition.
            type: getBasicIntrinsicTypes() symbol.
            value: constantValue
        }.
    };

    withSelector: #call:withArguments: addMethod: {
        | $(MidFunctionBuilder)self $(MidValue)functional $(Array)arguments :: MidCallFunction |
        $node := MidCallFunction#{
            previousControl: self currentControlNode.
            sourcePosition: self sourcePosition.
            functional: functional.
            arguments: arguments.
        }.
        node connectWithUsed.
        self currentControlNode: node.
        node
    };    
    withSelector: #send:to:withArguments: addMethod: {
        | $(MidFunctionBuilder)self $(MidValue)selector $(MidValue)receiver $(Array)arguments  :: MidSendMessage |
        $node := MidSendMessage#{
            previousControl: self currentControlNode.
            sourcePosition: self sourcePosition.
            receiver: receiver.
            selector: selector.
            arguments: arguments.
        }.
        node connectWithUsed.
        self currentControlNode: node.
        node
    };

    withSelector: #finish addMethod: {
        | $(MidFunctionBuilder)self :: Void |
        self midFunction stopNode: MidStopNode#{
            exitPoints: self exitPoints asArray
        }
    }.
}


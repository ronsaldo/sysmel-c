namespace: SysmelC definition: {
public: [
    $MakeNamespaceNamed($(Symbol)name :: Namespace) := Namespace#{
        name: name.
        exportedObjectTable: SymbolHashtable().
        exportedObjectList: OrderedCollection().
    }.

    Record: Module withFields: #{
        name: Symbol.
        globalNamespace: Namespace.
        mainEntryPoint: Function.
        exportedObjectSet: IdentityHashset.
        exportedObjectList: OrderedCollection.
        exportedTypeList: OrderedCollection.
        exportedNamespaceList: OrderedCollection.
        exportedCompiledFunctionList: OrderedCollection.
    }.

    ## The different kinds of Environment
    Inductive: Environment withAlternatives: [
        Record: EmptyEnvironment withFields: #{}.
        Record: IntrinsicsEnvironment withFields: #{
            parent: Environment.
            localSymbols: SymbolHashtable.
        }.
        Record: ModuleEnvironment withFields: #{
            parent: Environment.
            module: Module.
        }.
        Record: NamespaceEnvironment withFields: #{
            parent: Environment.
            namespace: Namespace
        }.
        Record: LexicalEnvironment withFields: #{
            parent: Environment.
            localSymbols: SymbolHashtable.
        }.
        Record: FunctionAnalysisEnvironment withFields: #{
            parent: Environment.
            localSymbols: SymbolHashtable.
        }.
    ].
    
    ## Type
    Record: SysmelTypeCommon withFields: #{
        name: Symbol.
        printingSuffix: String.
        isValueType: Boolean.
        valueAlignment: UInt32.
        valueSize: UInt32.
        heapAlignment: UInt32.
        heapSize: UInt32.
        methodDict: SymbolHashtable.
    }.

    Inductive: SysmelType withAlternatives: [
        Record: SysmelBasicType withFields: #{
            common: SysmelTypeCommon.
            supertype: SysmelType.
        }.
        Record: SysmelGradualType withFields: #{
            common: SysmelTypeCommon.
        }.
        Record: SysmelTupleAndRecordType withFields: #{
            common: SysmelTypeCommon.
            supertype: SysmelType.
            fields: Array.
            fieldNames: Array
        }.
        Record: SysmelClassType withFields: #{
            common: SysmelTypeCommon.
            supertype: SysmelType.
            superFieldCount: Integer.
            fields: Array.
            fieldNames: Array
        }.
        Record: SysmelEnumType withFields: #{
            common: SysmelTypeCommon.
            supertype: SysmelType.
            valueCount: UInt32.
            values: Array.
            valueNames: Array.
        }.
        Record: SysmelSumType withFields: #{
            common: SysmelTypeCommon.
            supertype: SysmelType.
            alternativeCount: UInt32.
            alternatives: Array.
        }.
        Record: SysmelSimpleFunctionType withFields: #{
            common: SysmelTypeCommon.
            supertype: SysmelType.
            isCompileTimePure: Boolean.
            isVariadic: Boolean.
            argumentTypes: Array.
            resultType: SysmelType.
        }.
        Record: SysmelPointerType withFields: #{
            common: SysmelTypeCommon.
            supertype: SysmelType.
            baseType: SysmelType.
        }.
        Record: SysmelReferenceType withFields: #{
            common: SysmelTypeCommon.
            supertype: SysmelType.
            baseType: SysmelType.
        }.
    ].

    SysmelType
        withSelector: #getCommonSection addMethod: {
            |$(SysmelType)self :: SysmelTypeCommon|
            match: self ofType: SysmelType withPatterns: #{
                $(SysmelBasicType)basicType : basicType common.
                $(SysmelGradualType)gradualType : gradualType common.
                $(SysmelTupleAndRecordType)tupleAndRecord : tupleAndRecord common.
                $(SysmelSumType)sumType : sumType common.
                $(SysmelClassType)clazzType : clazzType common.
                $(SysmelEnumType)enumType : enumType common.
                $(SysmelSimpleFunctionType)simpleFunctionType : simpleFunctionType common.
                $(SysmelPointerType)pointerType : pointerType common.
                $(SysmelReferenceType)referenceType : referenceType common.
                _: {
                    printLine("getCommonSection: Unsupported type kind").
                    printLine(self).
                    abort()
                }
            }
        };
        withSelector: #getMethodDictionary addMethod: {
            |$(SysmelType)self :: SymbolHashtable|
            match: self ofType: SysmelType withPatterns: #{
                $(SysmelBasicType)basicType : basicType common methodDict.
                $(SysmelGradualType)gradualType : gradualType common methodDict.
                $(SysmelTupleAndRecordType)tupleAndRecord : tupleAndRecord common methodDict.
                $(SysmelSumType)sumType : sumType common methodDict.
                $(SysmelClassType)clazzType : clazzType common methodDict.
                $(SysmelEnumType)enumType : enumType common methodDict.
                $(SysmelSimpleFunctionType)simpleFunctionType : simpleFunctionType common methodDict.
                $(SysmelPointerType)pointerType : pointerType common methodDict.
                $(SysmelReferenceType)referenceType : referenceType common methodDict.
                _: {
                    printLine("getCommonSection: Unsupported type kind").
                    printLine(self).
                    abort()
                }
            }
        };
        withSelector: #getSupertype addMethod: {
            |$(SysmelType)self :: SysmelType|
            match: self ofType: SysmelType withPatterns: #{
                $(SysmelBasicType)basicType : basicType supertype.
                $(SysmelGradualType)gradualType : null.
                $(SysmelTupleAndRecordType)tupleAndRecord : tupleAndRecord supertype.
                $(SysmelClassType)clazzType : clazzType supertype.
                $(SysmelSumType)sumType : sumType supertype.
                $(SysmelEnumType)enumType : enumType supertype.
                $(SysmelSimpleFunctionType)simpleFunctionType : simpleFunctionType supertype.
                $(SysmelReferenceType)referenceType : referenceType supertype.
                $(SysmelPointerType)pointerType : pointerType supertype.
                _: {
                    printLine("getSupertype: Unsupported type kind").
                    abort()
                }
            }
        };
        withSelector: #getBaseType addMethod: {
            |$(SysmelType)self :: SysmelType|
            match: self ofType: SysmelType withPatterns: #{
                $(SysmelEnumType)enumType : enumType supertype.
                $(SysmelReferenceType)referenceType : referenceType baseType.
                $(SysmelPointerType)pointerType : pointerType baseType.
                _: {
                    printLine("getBaseType: Unsupported type kind").
                    abort()
                }
            }
        };
        withSelector: #pointer addMethod: {
            |$(SysmelType)self :: SysmelType|
            SysmelType(SysmelPointerType#{
                common: SysmelTypeCommon#{
                    isValueType: true.
                    valueAlignment: 8u32.
                    valueSize: 8u32.
                    heapAlignment: 8u32.
                    heapSize: 8u32.
                    methodDict: SymbolHashtable().
                }.
                baseType: self
            }).
        };
        withSelector: #ref addMethod: {
            |$(SysmelType)self :: SysmelType|
            SysmelType(SysmelReferenceType#{
                common: SysmelTypeCommon#{
                    isValueType: true.
                    valueAlignment: 8u32.
                    valueSize: 8u32.
                    heapAlignment: 8u32.
                    heapSize: 8u32.
                    methodDict: SymbolHashtable().
                }.
                baseType: self
            }).
        }.

    Class: MidValue withFields: #{
        sourcePosition: SourcePosition.
        users: OrderedCollection.
        type: SysmelType.
        scheduleIndex: UInt32.
    }.

    Inductive: SysmelFunction withAlternatives: [
        Record: SysmelIntrinsicPrimitive withFields: #{
            name: Symbol.
            implementation: Function.
            isMacro: Boolean.
        }.
        Record: SysmelCompiledFunction withFields: #{
            name: Symbol.
            midIR: MidValue.
            isMacro: Boolean.
        }.
    ].

    Record: SemanticsState withFields: #{
        currentExpectedType: SysmelType
    }.

    Record: MacroContext withFields: #{
        sourcePosition: SourcePosition.
        environment: Environment.
        semanticsState: SemanticsState.
    }.
    
    Record: Record withFields: #{
        type: SysmelType.
        tuple: Tuple.
    }.

    Record: Object withFields: #{
        class: SysmelClassType.
        tuple: Tuple.
    }.

    ## A value with its different alternatives.
    Inductive: Value withAlternatives: [
        Record: InvalidErrorValue withFields: #{
            message: String
        }.
        Record: NullValue withFields: #{}.
        Record: VoidValue withFields: #{}.
        Record: IntegerValue withFields: #{
            value: Integer
        }.
        Record: BooleanValue withFields: #{
            value: Boolean
        }.
        Record: FloatValue withFields: #{
            value: Float
        }.
        Record: CharacterValue withFields: #{
            value: Character
        }.
        Record: StringValue withFields: #{
            value: String
        }.
        Record: SymbolValue withFields: #{
            value: Symbol
        }.
        Record: EnvironmentValue withFields: #{
            value: Environment
        }.
        Record: ParseTreeNodeValue withFields: #{
            value: ParseTreeNode
        }.
        Record: NamespaceValue withFields: #{
            value: Namespace
        }.
        Record: ModuleValue withFields: #{
            value: Module
        }.
        Record: SysmelTypeValue withFields: #{
            value: SysmelType
        }.
        Record: SysmelFunctionValue withFields: #{
            value: SysmelFunction
        }.
        Record: ArrayValue withFields: #{
            value: Array
        }.
        Record: TupleValue withFields: #{
            value: Tuple
        }.
        Record: RecordValue withFields: #{
            value: Record
        }.
        Record: ObjectValue withFields: #{
            value: Object
        }.
        Record: SumValue withFields: #{
            index: Integer.
            value: Value.
        }.
        Record: AssociationValue withFields: #{
            value: Association.
        }.
        Record: ImmutableDictionaryValue withFields: #{
            value: ImmutableDictionary.
        }.
        Record: GradualValue withFields: #{
            value: Gradual.
        }.
        Record: SysmelMidValue withFields: #{
            value: MidValue
        }.
        Record: SysmelValueBox withFields: #{
            value: Value
        }.
    ].
    $extractValueForAnalysis($(Value)value :: Gradual) := {
        match: value ofType: Value withPatterns: #{
            $(IntegerValue)value : value value.
            $(BooleanValue)value : value value.
            $(FloatValue)value : value value.
            $(CharacterValue)value : value value.
            $(StringValue)value : value value.
            $(SymbolValue)value : value value.
            $(ArrayValue)value : value value.
            $(TupleValue)value : value value.
            $(AssociationValue)value : value value.
            $(ImmutableDictionaryValue)value : value value.
            $(GradualValue)value : value value.
            _: {
                value
            }
        }
    }.
    $extractValueForPrinting($(Value)value :: Gradual) := {
        match: value ofType: Value withPatterns: #{
            $(IntegerValue)value : value value.
            $(BooleanValue)value : value value.
            $(FloatValue)value : value value.
            $(CharacterValue)value : value value.
            $(StringValue)value : value value.
            $(SymbolValue)value : value value.
            $(ArrayValue)value : value value.
            $(TupleValue)value : value value.
            $(RecordValue)value : value value tuple.
            $(AssociationValue)value : value value.
            $(ImmutableDictionaryValue)value : value value.
            $(GradualValue)value : value value.
            $(NamespaceValue)value : value value.
            $(SumValue)value : (value index, value value).
            $(SysmelTypeValue)value : {
                $name := value value getCommonSection name.
                if: name isNull then: {
                    value value
                } else: {
                    name
                }
            }.
            _: {
                value
            }
        }
    }.

    ## Typed value
    Record: TypedValue withFields: #{
        value: Value.
        type: SysmelType
    }.
        
    Value
        withSelector: #isInvalidErrorValue addMethod: {
            |$(Value)self :: Boolean |
            match: self ofType: Value withPatterns: #{
                $(InvalidErrorValue)errorValue : true.
                _: false.
            }
        };
        withSelector: #getInvalidErrorValueMessage addMethod: {
            |$(Value)self :: String |
            match: self ofType: Value withPatterns: #{
                $(InvalidErrorValue)errorValue : errorValue message.
                _: abort().
            }
        };
        withSelector: #isMidValue addMethod: {
            |$(Value)self :: Boolean |
            match: self ofType: Value withPatterns: #{
                $(SysmelMidValue)midValue : true.
                _: false.
            }
        };
        withSelector: #getMidValue addMethod: {
            |$(Value)self :: MidValue |
            match: self ofType: Value withPatterns: #{
                $(SysmelMidValue)midValue : midValue value.
                _: abort().
            }
        };
        withSelector: #getIntegerValue addMethod: {
            |$(Value)self :: Integer |
            match: self ofType: Value withPatterns: #{
                $(IntegerValue)integerValue : integerValue value.
                _: abort().
            }.
        };
        withSelector: #getTypeValue addMethod: {
            |$(Value)self :: SysmelType |
            match: self ofType: Value withPatterns: #{
                $(SysmelTypeValue)typeValue : typeValue value.
                _: abort().
            }.
        };
        withSelector: #getSumIndex addMethod: {
            |$(Value)self :: SysmelType |
            match: self ofType: Value withPatterns: #{
                $(SumValue)sumValue : sumValue index.
                _: abort().
            }.
        };
        withSelector: #getSumValue addMethod: {
            |$(Value)self :: SysmelType |
            match: self ofType: Value withPatterns: #{
                $(SumValue)sumValue : sumValue value.
                _: abort().
            }.
        };
        withSelector: #isObjectValue addMethod: {
            |$(Value)self :: Boolean |
            match: self ofType: Value withPatterns: #{
                $(ObjectValue)objectValue : true.
                _: false
            }.
        };
        withSelector: #getObjectValue addMethod: {
            |$(Value)self :: Object |
            match: self ofType: Value withPatterns: #{
                $(ObjectValue)objectValue : objectValue value.
                _: abort
            }.
        };
        withSelector: #isTypedValue addMethod: {
            |$(Value)self :: Boolean |
            false
        }.

    TypedValue
        withSelector: #isInvalidErrorValue addMethod: {
            |$(TypedValue)self :: Boolean |
            self value isInvalidErrorValue
        };
        withSelector: #getInvalidErrorValueMessage addMethod: {
            |$(TypedValue)self :: String |
            self value getInvalidErrorValueMessage
        };
        withSelector: #getIntegerValue addMethod: {
            |$(TypedValue)self :: Integer |
            if: self value isNull then: {
                return: 0
            }
            else: 
            {
                self value getIntegerValue
            }
        };
        withSelector: #isTypedValue addMethod: {
            |$(TypedValue)self :: Boolean |
            true
        };
        withSelector: #isMidValue addMethod: {
            |$(TypedValue)self :: Boolean |
            false
        }.

SysmelClassType
    withSelector: #getFieldTypeAtIndex: addMethod: {
        | $(SysmelClassType)self $(Integer)index :: SysmelType |
        if: index >= self superFieldCount then: {
            self fields at: index - self superFieldCount
        }
        else:
        {
            match: self supertype ofType: SysmelType withPatterns: #{
                $(SysmelClassType)superclass : {
                    return: (superclass getFieldTypeAtIndex: index)
                }.
                _: {}
            }.
            
            printLine("There is no field at index ". index)
        }
    };
    withSelector: #totalFieldCount addMethod: {
        | $(SysmelClassType)self :: Integer |
        self superFieldCount + self fields size  
    }.

    SysmelFunction
        withSelector: #getName addMethod: {
            | $(SysmelFunction) self :: Symbol |
            match:self ofType: SysmelFunction withPatterns: #{
                $(SysmelIntrinsicPrimitive)intrinsic : intrinsic name.
                $(SysmelCompiledFunction)compiled : compiled name.
            }
        }.
    SysmelFunction
        withSelector: #setName: addMethod: {
            | $(SysmelFunction)self $(Symbol) newName :: Void  |
            match:self ofType: SysmelFunction withPatterns: #{
                $(SysmelIntrinsicPrimitive)intrinsic :{
                    intrinsic name: newName
                }.
                $(SysmelCompiledFunction)compiled : {
                    compiled name: newName.
                }
            }
        }.
    $extractArrayFromValue($(Value)value :: Array) := {
        match: value ofType: Value withPatterns: #{
            $(ArrayValue)arrayValue : arrayValue value.
            _: {
                printLine("Not a valid array value").
                abort()
            }
        }.
    }.
    $extractTupleFromValue($(Value)value :: Tuple) := {
        match: value ofType: Value withPatterns: #{
            $(TupleValue)arrayValue : arrayValue value.
            _: {
                printLine("Not a valid tuple value").
                abort()
            }
        }.
    }.
    $extractAssociationFromValue($(Value)value :: Association) := {
        match: value ofType: Value withPatterns: #{
            $(AssociationValue)association : association value.
            _: {
                printLine("Not a valid association value").
                abort()
            }
        }.
    }.
    $isValueWithImmutableDictionaryFromValue($(Value)value :: Boolean) := {
        match: value ofType: Value withPatterns: #{
            $(ImmutableDictionaryValue)dict : true.
            _: false
        }.
    }.
    $extractImmutableDictionaryFromValue($(Value)value :: ImmutableDictionary) := {
        match: value ofType: Value withPatterns: #{
            $(ImmutableDictionaryValue)dict : dict value.
            _: {
                printLine("Not a valid association value").
                abort()
            }
        }.
    }.
    $valueContainsParseTreeFromValue($(Value)value :: Boolean) := {
        match: value ofType: Value withPatterns: #{
            $(ParseTreeNodeValue)parseTreeValue : true.
            _: false.
        }.
    }.
    $extractParseTreeNodeFromValue($(Value)value :: ParseTreeNode) := {
        match: value ofType: Value withPatterns: #{
            $(ParseTreeNodeValue)parseTreeValue : parseTreeValue value.
            _: {
                printLine("Not a valid array value").
                abort()
            }
        }.
    }.
    $extractBooleanFromValue($(Value)value :: Boolean) := {
        match: value ofType: Value withPatterns: #{
            $(BooleanValue)booleanValue : booleanValue value.
            _: {
                printLine("Not a valid boolean value").
                abort()
            }
        }.
    }.
    $extractIntegerFromValue($(Value)value :: String) := {
        match: value ofType: Value withPatterns: #{
            $(IntegerValue)integerValue : integerValue value.
            _: {
                printLine("Not a valid string value.").
                abort()
            }
        }.
    }.
    $extractStringFromValue($(Value)value :: String) := {
        match: value ofType: Value withPatterns: #{
            $(StringValue)stringValue : stringValue value.
            _: {
                printLine("Not a valid string value.").
                abort()
            }
        }.
    }.
    $extractSymbolFromValue($(Value)value :: Symbol) := {
        match: value ofType: Value withPatterns: #{
            $(SymbolValue)symbolValue : symbolValue value.
            _: {
                printLine("Not a valid symbol value").
                abort()
            }
        }.
    }.
    $extractGradualFromValue($(Value)value :: Gradual) := {
        match: value ofType: Value withPatterns: #{
            $(GradualValue)gradualValue : gradualValue value.
            _: {
                printLine("Not a valid gradual value.").
                abort()
            }
        }.
    }.
    $extractTypeFromValue($(Value)value :: SysmelType) := {
        match: value ofType: Value withPatterns: #{
            $(SysmelTypeValue)typeValue : typeValue value.
            _: {
                printLine("Not a valid sysmel type value").
                abort()
            }
        }.
    }.
    $isTypeValue($(Value)value :: Boolean) := {
        match: value ofType: Value withPatterns: #{
            $(SysmelTypeValue)typeValue : true.
            _: false.
        }.
    }.
    $extractBoxFromValue($(Value)value :: SysmelValueBox) := {
        match: value ofType: Value withPatterns: #{
            $(SysmelValueBox)valueBox : valueBox.
            _: {
                printLine("Not a valid sysmel box value").
                abort()
            }
        }.
    }.
    $isValueBoxValue($(Value)value :: Boolean) := {
        match: value ofType: Value withPatterns: #{
            $(SysmelValueBox)typeValue : true.
            _: false.
        }.
    }.
    $extractNamespaceFromValue($(Value)value :: Namespace) := {
        match: value ofType: Value withPatterns: #{
            $(NamespaceValue)namespaceValue : namespaceValue value.
            _: {
                printLine("Not a valid sysmel namespace value").
                abort()
            }
        }.
    }.

    $isNamespaceValue($(Value)value :: Boolean) := {
        match: value ofType: Value withPatterns: #{
            $(NamespaceValue)namespaceValue : true.
            _: false.
        }.
    }.
    $extractFunctionFromValue($(Value)value :: SysmelFunction) := {
        match: value ofType: Value withPatterns: #{
            $(SysmelFunctionValue)functionValue : functionValue value.
            _: {
                printLine("Not a valid function value").
                abort()
            }
        }
    }.
    $isFunctionValue($(Value)value :: Boolean) := {
        match: value ofType: Value withPatterns: #{
            $(SysmelFunctionValue)functionValue : true.
            _: false.
        }
    }.
    $sysmelFunctionIsMacro($(SysmelFunction)function :: Boolean) := {
        match: function ofType: SysmelFunction withPatterns: #{
            $(SysmelIntrinsicPrimitive)intrinsic : intrinsic isMacro.
            $(SysmelCompiledFunction)compiled : compiled isMacro.
        }
    }.

    $isRecordType($(SysmelType)type :: Boolean) := {
        match: type ofType: SysmelType withPatterns: #{
            $(SysmelTupleAndRecordType)tupleAndRecord : true.
            _: false.
        }
    }.

    $extractRecordType($(SysmelType)type :: SysmelTupleAndRecordType) := {
        match: type ofType: SysmelType withPatterns: #{
            $(SysmelTupleAndRecordType)tupleAndRecord : tupleAndRecord.
            _: {
                printLine("Not a valid record type.").
                abort()
            }
        }
    }.

    $extractRecordFromValue($(Value)value :: Record) := {
        match: value ofType: Value withPatterns: #{
            $(RecordValue)recordValue : recordValue value.
            _: {
                printLine("Not a valid record value").
                abort()
            }
        }
    }.

    $isGradualType($(SysmelType)type :: Boolean) := {
        match: type ofType: SysmelType withPatterns: #{
            $(SysmelGradualType)classType : true.
            _: false.
        }
    }.

    $extractGradualType($(SysmelType)type :: SysmelClassType) := {
        match: type ofType: SysmelType withPatterns: #{
            $(SysmelGradualType)classType : classType.
            _: {
                printLine("Not a valid class type.").
                abort()
            }
        }
    }.

    $isSimpleFunctionType($(SysmelType)type :: Boolean) := {
        match: type ofType: SysmelType withPatterns: #{
            $(SysmelSimpleFunctionType)classType : true.
            _: false.
        }
    }.

    $extractSimpleFunctionType($(SysmelType)type :: SysmelSimpleFunctionType) := {
        match: type ofType: SysmelType withPatterns: #{
            $(SysmelSimpleFunctionType)functionType : functionType.
            _: {
                printLine("Not a valid class type.").
                abort()
            }
        }
    }.

    $isCompileTimePure($(SysmelType)type :: Boolean) := {
        match: type ofType: SysmelType withPatterns: #{
            $(SysmelSimpleFunctionType)functionType : functionType isCompileTimePure.
            _: { false }
        }
    }.
    

    $isClassType($(SysmelType)type :: Boolean) := {
        match: type ofType: SysmelType withPatterns: #{
            $(SysmelClassType)classType : true.
            _: false.
        }
    }.

    $extractClassType($(SysmelType)type :: SysmelClassType) := {
        match: type ofType: SysmelType withPatterns: #{
            $(SysmelClassType)classType : classType.
            _: {
                printLine("Not a valid class type.").
                abort()
            }
        }
    }.

    $isSumType($(SysmelType)type :: Boolean) := {
        match: type ofType: SysmelType withPatterns: #{
            $(SysmelSumType)SysmelSumType : true.
            _: false.
        }
    }.
    
    $extractSumType($(SysmelType)type :: SysmelSumType) := {
        match: type ofType: SysmelType withPatterns: #{
            $(SysmelSumType)sumType : sumType.
            _: {
                printLine("Not a valid sum type.").
                abort()
            }
        }
    }.

    $extractObjectFromValue($(Value)value :: Object) := {
        match: value ofType: Value withPatterns: #{
            $(ObjectValue)objectValue : objectValue value.
            _: {
                printLine("Not a valid object value").
                abort()
            }
        }
    }.
    $isReferenceType($(SysmelType)type :: Boolean) := {
        match: type ofType: SysmelType withPatterns: #{
            $(SysmelReferenceType)refType : true.
            _: false.
        }
    }.
    $extractReferenceType($(SysmelType)type :: SysmelReferenceType) := {
        match: type ofType: SysmelType withPatterns: #{
            $(SysmelReferenceType)refType : refType.
            _: {
                printLine("Not a valid reference type.").
                abort().
            }
        }
    }.
    $isPointerType($(SysmelType)type :: Boolean) := {
        match: type ofType: SysmelType withPatterns: #{
            $(SysmelPointerType)refType : true.
            _: false.
        }
    }.
    $extractPointerType($(SysmelType)type :: SysmelPointerType) := {
        match: type ofType: SysmelType withPatterns: #{
            $(SysmelPointerType)refType : refType.
            _: {
                printLine("Not a valid reference type.").
                abort().
            }
        }
    }.

    $findIndexOfRecordFieldWithName($(SysmelTupleAndRecordType)recordType $(Symbol)fieldName :: Integer) := {
        $fieldCount := recordType fieldNames size.
        $!i := 0. while: (i < fieldCount) do: {
            $recordFieldName := recordType fieldNames at: i.
            if: fieldName == recordFieldName then:{
                return: i
            }.

        } continueWith:(i := i + 1).

        return: 1 negated.
    }.
    $findIndexOfClassFieldWithName($(SysmelClassType)classType $(Symbol)fieldName :: Integer) := {
        $fieldCount := classType fieldNames size.
        $!i := 0. while: (i < fieldCount) do: {
            $classFieldName := classType fieldNames at: i.
            if: fieldName == classFieldName then:{
                return: classType superFieldCount + i 
            }.

        } continueWith:(i := i + 1).

        if: classType superFieldCount > 0 then: {
            match: classType supertype ofType: SysmelType withPatterns: #{
                $(SysmelClassType)superclass : {
                    return: findIndexOfClassFieldWithName(superclass. fieldName).
                }
            }
        }.

        return: 1 negated.
    }.
    $findIndexOfEnumValueWithName($(SysmelEnumType)enumType $(Symbol)valueName :: Integer) := {
        $valueCount := enumType valueCount.
        $!i := 0. while: (i < valueCount) do: {
            $enumValueName := enumType valueNames at: i.
            if: valueName == enumValueName then:{
                return: i
            }.

        } continueWith:(i := i + 1).

        return: 1 negated.
    }.
    $findIndexOfSumTypeAlternative($(SysmelSumType)sumType $(SysmelType)expectedType :: Integer) := {
        $alternativeCount := sumType alternativeCount.
        $!i := 0. while: (i < alternativeCount) do: {
            $alternative := sumType alternatives at: i.
            if: alternative == expectedType then:{
                return: i
            }.
        } continueWith:(i := i + 1).

        return: 1 negated.
    }
]
}

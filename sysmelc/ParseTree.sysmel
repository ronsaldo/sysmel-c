namespace: SysmelC definition: {
public: [
    Inductive: ParseTreeNode withAlternatives: [
        Record: ParseTreeNullNode withFields: #{}.

        ## Errors.
        Record: ParseTreeErrorNode withFields: #{
            sourcePosition: SourcePosition.
            errorMessage: String.
            innerNodes: ParseTreeNode
        }.
        Record: ParseTreeAssertNode withFields: #{
            sourcePosition: SourcePosition.
            errorMessage: String.
            innerNodes: ParseTreeNode
        }.
        
        ## Literals
        Record: ParseTreeLiteralIntegerNode withFields: #{
            sourcePosition: SourcePosition.
            value: Integer.
        }.
        Record: ParseTreeLiteralCharacterNode withFields: #{
            sourcePosition: SourcePosition.
            value: Character.
        }.
        Record: ParseTreeLiteralFloatNode withFields: #{
            sourcePosition: SourcePosition.
            value: Float.
        }.
        Record: ParseTreeLiteralStringNode withFields: #{
            sourcePosition: SourcePosition.
            value: String.
        }.
        Record: ParseTreeLiteralSymbolNode withFields: #{
            sourcePosition: SourcePosition.
            value: Symbol.
        }.
        
        ## Identifiers
        Record: ParseTreeIdentifierReferenceNode withFields: #{
            sourcePosition: SourcePosition.
            value: Symbol.
        }.

        ## Functions and message send
        Record: ParseTreeFunctionApplicationNode withFields: #{
            sourcePosition: SourcePosition.
            functional: ParseTreeNode.
            arguments: Array
        }.
        Record: ParseTreeMessageSendNode withFields: #{
            sourcePosition: SourcePosition.
            receiver: ParseTreeNode.
            selector: ParseTreeNode.
            arguments: Array
        }.
        Record: ParseTreeMessageCascadeNode withFields: #{
            sourcePosition: SourcePosition.
            receiver: ParseTreeNode.
            cascadedMessages: Array.
        }.
        Record: ParseTreeCascadedMessageNode withFields: #{
            sourcePosition: SourcePosition.
            selector: ParseTreeNode.
            arguments: Array
        }.
        Record: ParseTreeBinaryOperatorSequenceNode withFields: #{
            sourcePosition: SourcePosition.
            elements: Array
        }.

        ## Sequences, array, tuples
        Record: ParseTreeSequenceNode withFields: #{
            sourcePosition: SourcePosition.
            elements: Array
        }.
        Record: ParseTreeTupleNode withFields: #{
            sourcePosition: SourcePosition.
            elements: Array
        }.
        Record: ParseTreeArrayNode withFields: #{
            sourcePosition: SourcePosition.
            elements: Array
        }.
        Record: ParseTreeByteArrayNode withFields: #{
            sourcePosition: SourcePosition.
            elements: Array
        }.

        ## ImmutableDictionary.
        Record: ParseTreeAssociationNode withFields: #{
            sourcePosition: SourcePosition.
            key: ParseTreeNode.
            value: ParseTreeNode.
        }.
        Record: ParseTreeImmutableDictionaryNode withFields: #{
            sourcePosition: SourcePosition.
            elements: Array.
        }.

        ## Blocks
        Record: ParseTreeBlockClosureNode withFields: #{
            sourcePosition: SourcePosition.
            functionType: ParseTreeNode.
            body: ParseTreeNode.
        }.
        Record: ParseTreeLexicalBlockNode withFields: #{
            sourcePosition: SourcePosition.
            expression: ParseTreeNode
        }.

        ## Macro operators
        Record: ParseTreeQuoteNode withFields: #{
            sourcePosition: SourcePosition.
            expression: ParseTreeNode
        }.
        Record: ParseTreeQuasiQuoteNode withFields: #{
            sourcePosition: SourcePosition.
            expression: ParseTreeNode
        }.
        Record: ParseTreeQuasiUnquoteNode withFields: #{
            sourcePosition: SourcePosition.
            expression: ParseTreeNode
        }.
        Record: ParseTreeSpliceNode withFields: #{
            sourcePosition: SourcePosition.
            expression: ParseTreeNode
        }.

        ## Binding and pattern matching
        Record: ParseTreeFunctionalDependentTypeNode withFields: #{
            sourcePosition: SourcePosition.
            arguments: Array.
            resultTypeExpression: ParseTreeNode
        }.
        Record: ParseTreeBindableNameNode withFields: #{
            sourcePosition: SourcePosition.
            typeExpression: ParseTreeNode.
            nameExpression: ParseTreeNode.
            isAnonymous: Boolean.
            isImplicit: Boolean.
            isExistential: Boolean.
            isVariadic: Boolean.
            isMutable: Boolean.
            hasPostTypeExpression: Boolean.
        }.
        Record: ParseTreeAssignmentNode withFields: #{
            sourcePosition: SourcePosition.
            store: ParseTreeNode.
            value: ParseTreeNode.
        }.

        ## Closure and functions.
        Record: ParseTreeFunctionNode withFields: #{
            sourcePosition: SourcePosition.
            functionDependentType: ParseTreeNode.
            bodyExpression: ParseTreeNode.
            name: Symbol.
            isAnonymous: Boolean
        }.

        ## Control flow. Exposed via macros
        Record: ParseTreeIfSelectionNode withFields: #{
            sourcePosition: SourcePosition.
            condition: ParseTreeNode.
            trueExpression: ParseTreeNode.
            falseExpression: ParseTreeNode
        }.
        Record: ParseTreeWhileLoopNode withFields: #{
            sourcePosition: SourcePosition.
            condition: ParseTreeNode.
            body: ParseTreeNode.
            continueExpression: ParseTreeNode
        }.
        Record: ParseTreeDoWhileLoopNode withFields: #{
            sourcePosition: SourcePosition.
            body: ParseTreeNode.
            continueExpression: ParseTreeNode.
            condition: ParseTreeNode
        }.
        Record: ParseTreeSwitchNode withFields: #{
            sourcePosition: SourcePosition.
            value: ParseTreeNode.
            cases: ParseTreeNode.
        }.
        Record: ParseTreeSwitchPatternMatchingNode withFields: #{
            sourcePosition: SourcePosition.
            value: ParseTreeNode.
            valueSumType: ParseTreeNode.
            cases: ParseTreeNode
        }.
        Record: ParseTreeReturnNode withFields: #{
            sourcePosition: SourcePosition.
            valueExpression: ParseTreeNode.
        }.

        ## Namespaces
        Record: ParseTreeNamespaceDefinitionNode withFields: #{
            sourcePosition: SourcePosition.
            namespace: Namespace.
            definition: ParseTreeNode.
        }.
    ].

$getSourcePositionFromNode($(ParseTreeNode)node :: SourcePosition) := {
    match: node ofType: ParseTreeNode withPatterns: #{
        _: SourcePosition().
    }
}.
].

ParseTreeNode
    withSelector: #isFunctionalDependentTypeNode addMethod: {
        |$(ParseTreeNode)self :: Boolean | 
        match: self ofType: ParseTreeNode withPatterns: #{
            $(ParseTreeFunctionalDependentTypeNode)funtype : true.
            _: false
        }
    };
    withSelector: #extractFunctionalDependentTypeNode addMethod: {
        |$(ParseTreeNode)self :: ParseTreeFunctionalDependentTypeNode | 
        match: self ofType: ParseTreeNode withPatterns: #{
            $(ParseTreeFunctionalDependentTypeNode)funtype : funtype.
            _: abort()
        }
    };
    withSelector: #extractBindableNameNode addMethod: {
        |$(ParseTreeNode)self :: ParseTreeBindableNameNode | 
        match: self ofType: ParseTreeNode withPatterns: #{
            $(ParseTreeBindableNameNode)bindable : bindable.
            _: abort()
        }
    }.
    
}